buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
            name = 'jboss'
            url = 'http://repository.jboss.org/nexus/content/groups/public/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'org.hibernate.build.gradle:gradle-maven-publish-auth:2.0.1'
    }
}

// Apply plugins
apply plugin: 'forge'
apply plugin: 'maven-publish'
apply plugin: 'maven-publish-auth'

// Basic project information
group = 'de.unratedfilms'
version = '1.2.0'

// Minimum java version
sourceCompatibility = 1.8
targetCompatibility = 1.8

minecraft {
    version = '1.7.10-10.13.4.1614-1.7.10'
    runDir = 'run'

    // Replace strings with constants in the source
    replace "@MOD_VERSION@", project.version
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// declaring new configuration that will be used
// to associate with artifacts
configurations {
    library
}

// Task for creating a deobfuscated JAR;
// Needed by the projects which use this library as a mod
task deobfJar(type: Jar) {
    tasks.reobf.shouldRunAfter deobfJar
    from sourceSets.main.output
    classifier = 'dev'
}

// Task for creating a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = 'sources'
}

// Task for creating a javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

// Disable javadoc warnings for java 8
if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }
}

// Task for creating a deobfuscated jar
// that can be shaded into other mod
task universalLibraryJar(type: Jar) {
    tasks.reobf.shouldRunAfter universalLibraryJar
    from(sourceSets.main.output) {
        exclude 'mcmod.info'
    }
    classifier = 'universal'
}

artifacts {
    archives jar
    library universalLibraryJar
    library sourceJar
    library javadocJar
}

// Simplify the process of creating library that can be shaded into other mod
// just run :
//  ./gradlew buildLib
//  ./gradlew buildLib -Poutput="../my/libs/folder/"
task buildLib(type: Copy, dependsOn: [universalLibraryJar, sourceJar, javadocJar, build]) {

    if(project.hasProperty('output')) {
        from universalLibraryJar
        from sourceJar
        from javadocJar
        from jar
        into "${project.output}/${project.property('name')}/${version}"
    }
}

// Publish the project artifacts to a maven repository
publishing {
    publications {
        maven(MavenPublication) {
            artifact jar
            artifact deobfJar
            artifact sourceJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            name = 'quartercode-deployment'
            url = version.endsWith('-SNAPSHOT') ?
                    'http://repo.quartercode.com/content/repositories/snapshots/' :
                    'http://repo.quartercode.com/content/repositories/releases/'
        }
    }
}
