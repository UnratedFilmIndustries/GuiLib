buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

// Apply plugins
apply plugin: 'forge'

// Basic project information
version = '1.1.1'
group = 'de.unratedfilms'
archivesBaseName = 'guilib'

// Minimum java version
sourceCompatibility = 1.7
targetCompatibility = 1.7

minecraft {
    version = '1.7.10-10.13.4.1614-1.7.10'
    runDir = 'run'
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// Task for creating a deobfuscated JAR; needed by the projects which use this library
task deobfJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.main.resources
    classifier = 'dev'
}

// Task for creating a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.java
    from sourceSets.main.resources
    classifier = 'sources'
}

// Task for creating a javadoc jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

// Project artifacts (binaries, deobf binaries, sources, and javadoc)
artifacts {
    archives jar
    archives deobfJar
    archives sourceJar
    archives javadocJar
}
